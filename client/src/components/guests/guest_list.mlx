open React

let copy_to_clipboard =
  [%mel.raw
    {|
  function(text) {
    return navigator.clipboard.writeText(text);
  }
|}]
;;

let[@react.component] make ~guests =
  let guests_list = Array.to_list guests in
  match guests_list with
  | [] -> <Empty_state />
  | _ ->
    <ul role="list"
        className="divide-y divide-gray-100 border border-gray-100 rounded-2xl">
      (Array.map
         (fun (guest : Api.Guests.t) ->
           let status =
             match guest.rsvp_status with
             | `not_invited -> "Not Invited"
             | `invited -> "Invited"
             | `accepted -> "Accepted"
             | `declined -> "Declined"
           in
           let color =
             match guest.rsvp_status with
             | `not_invited -> `gray
             | `invited -> `yellow
             | `accepted -> `green
             | `declined -> `red
           in
           <li key=guest.id
               className="flex items-center justify-between gap-x-6 p-4">
             <div className="min-w-0">
               <div className="flex items-start gap-x-3">
                 <p className="text-sm font-semibold leading-6 text-gray-900">
                   (string (guest.first_name ^ " " ^ guest.last_name))
                 </p>
                 <Ui.Badge label=status color />
               </div>
               <div className="mt-1 flex items-center gap-x-2 text-xs \
                               leading-5 text-gray-500">
                 <p className="whitespace-nowrap">
                   (string (guest.city ^ ", " ^ guest.state))
                 </p>
                 <svg viewBox="0 0 2 2" className="h-0.5 w-0.5 fill-current">
                   <circle r="1" cx="1" cy="1" />
                 </svg>
                 <p className="truncate">
                   (string (Utils.Date_fns.format_date_str guest.created_at))
                 </p>
               </div>
             </div>
             <div className="flex flex-none items-center gap-x-1">
               <Ui.Button_tooltip label="Copy Email"
                                  icon="email"
                                  on_click=(fun _ ->
                                             copy_to_clipboard guest.email) />
               <Edit_guest_dialog guest />
               <Delete_guest_dialog guest />
             </div>
           </li>)
         guests
       |> array)
    </ul>
;;
