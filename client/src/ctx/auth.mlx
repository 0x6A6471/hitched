open React

external api_url : string = "import.meta.env.VITE_API_URL"

type user =
  { id : string
  ; email : string
  ; name : string
  }

type login_data =
  { email : string
  ; password : string
  }

module Decode = struct
  let user json =
    Json.Decode.(
      json
      |> fun json ->
      let id = field "id" string json in
      let email = field "email" string json in
      let name = field "name" string json in
      { id; email; name })
  ;;
end

type context_value =
  { user : user option
  ; login : login_data -> unit Js.Promise.t
  ; is_loading : bool
  }

let init =
  { user = None; login = (fun _ -> Js.Promise.resolve ()); is_loading = true }
;;

let auth_context = createContext init

module Provider = struct
  include Context

  let make = provider auth_context
end

module AuthProvider = struct
  let[@react.component] make ~children =
    let user, set_user = useState (fun _ -> None) in
    let is_loading, set_is_loading = useState (fun _ -> true) in
    let login form_data =
      set_is_loading (fun _ -> true);
      let headers =
        let dict = Js.Dict.empty () in
        Js.Dict.set dict "Content-Type" "application/json";
        Fetch.HeadersInit.makeWithDict dict
      in
      let payload = Js.Dict.empty () in
      Js.Dict.set payload "email" (Js.Json.string form_data.email);
      Js.Dict.set payload "password" (Js.Json.string form_data.password);
      Js.Promise.(
        Fetch.fetchWithInit
          (api_url ^ "/api/auth/login")
          (Fetch.RequestInit.make
             ~method_:Post
             ()
             ~credentials:Include
             ~headers
             ~body:
               (Fetch.BodyInit.make
                  (Js.Json.stringify (Js.Json.object_ payload))))
        |> then_ Fetch.Response.json
        |> then_ (fun json ->
          (let user = json |> Decode.user in
           set_user (fun _ -> Some user));
          set_is_loading (fun _ -> false);
          ReasonReactRouter.push "/dashboard" |> resolve)
        |> catch (fun err ->
          Js.log2 "Error in AuthContext login: " err |> resolve))
    in
    let validate _ =
      Js.Promise.(
        Fetch.fetchWithInit
          (api_url ^ "/api/auth/validate")
          (Fetch.RequestInit.make () ~credentials:Include)
        |> then_ Fetch.Response.json
        |> then_ (fun json ->
          (let user = json |> Decode.user in
           set_user (fun _ -> Some user));
          set_is_loading (fun _ -> false) |> resolve)
        |> catch (fun err ->
          Js.log2 "Error in AuthContext login: " err;
          set_is_loading (fun _ -> false) |> resolve))
    in
    useEffect0 (fun () ->
      validate () |> ignore;
      None);
    let value = { user; login; is_loading } in
    <Provider value>children</Provider>
  ;;
end
